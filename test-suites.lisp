;;;
;;;  cl-test -- another test framework for common lisp.
;;;  Copyright (C) 2022  M E Leypold
;;;
;;;  This program is free software: you can redistribute it and/or modify
;;;  it under the terms of the GNU General Public License as published by
;;;  the Free Software Foundation, either version 3 of the License, or
;;;  (at your option) any later version.
;;;
;;;  This program is distributed in the hope that it will be useful,
;;;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;;;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;;  GNU General Public License for more details.
;;;
;;;  You should have received a copy of the GNU General Public License
;;;  along with this program.  If not, see <https://www.gnu.org/licenses/>.
;;;
;;;  For alternative licensing options, see README.md

(in-package :de.m-e-leypold.cl-test/loading-ramp)
(declaim (optimize (speed 0) (space 0) (compilation-speed 0) (debug 3) (safety 3)))

;;; * Package definition  --------------------------------------------------------------------------

(define-package :de.m-e-leypold.cl-test/test-suites

    "This package defines the `TEST-SUITE' abstraction.

     TODO: Explain more, refer to other packages.
    "
  (:export
   :with-new-suite-registry :do-suites :get-suites :get-suite :get-suite-tags
   :get-test-ids :do-test-ids
   :suite-symbol :suite-id
   :devar-suite-symbol))

(in-package :de.m-e-leypold.cl-test/test-suites)

;;; * Suite registry -------------------------------------------------------------------------------

(defparameter *suites-package*  (find-package "DE.M-E-LEYPOLD.CL-TEST/SUITES"))
(defparameter *suites*          '())
(defparameter *suite-instances* (make-hash-table))

(defmacro with-new-suite-registry ((&optional package) &body body)
  `(let ((*suites*          '())
	 (*suite-instances* (make-hash-table))
	 (*suites-package*  ,package))
     ,@body))

(defun get-suites ()
  (nreverse
   (mapcar #'(lambda (id) (gethash id *suite-instances*)) *suites*)))

(defmacro do-suites ((suite-var) &body body)
  `(progn
     (dolist (,suite-var (get-suites))
       ,@body)))

(defun defvar-suite-symbol (symbol)  
  (if *suites-package*
      (let* ((package (symbol-package symbol))
	     (package-name (package-name package))
	     (package-symbol (intern package-name *suites-package*)))
	`(defvar ,package-symbol nil))
      nil))
	

;;; * defclass TEST-SUITE  -------------------------------------------------------------------------

(defclass test-suite ()
  ((suite-id
    :type symbol
    :reader suite-id
    :initarg :suite-id
    :initform (error "SUITE-ID is required for TEST-SUITE")
    :documentation "ID of a suite, a symbol from package :KEYWORD") 
   (suite-symbol
    :type symbol
    :reader suite-symbol
    :initarg :suite-symbol
    :initform (error "SUITE-SYMBOL is required for TEST-SUITE")
    :documentation "Symbol in *SUITE-PACKAGE* to which the suite is bound")
   (suite-tests
    :type (or (cons symbol) null)
    :accessor suite-tests
    :initform '()
    :documentation "Tests in the suite, as symbols")))

;; TODO: Printing of test suites
;; TODO: Also capture package

(let ((keyword-package (find-package :keyword)))

  (defun get-or-create-suite (package)
    (let* ((package-name (package-name package))
	   (suite-id (intern package-name keyword-package))
	   (test-suite (gethash suite-id *suite-instances* nil)))

      ;; Do we have the suite already?

      (if test-suite
	  test-suite

	  ;; Otherwise we have to create a TEST-SUITE instance and bind it.  Note that the
	  ;; clause generated by DEFVAR-SUITE-SYMBOL will already have made the symbol special
	  ;; (if it exists at all), but we need to obtain the symbol again.
	  
	  (let* ((package-symbol
		   (if *suites-package*
		       (intern package-name *suites-package*)
		       nil)))

	    (if package-symbol
		(progn
		  ;; (proclaim `(special ,package-symbol))	   
		  (export (list package-symbol) *suites-package*)))
	    
	    #+nil (format t "Registering: ~S~%" package-symbol) ; TODO: Need a logging package
	    
	    (let ((test-suite
		    (make-instance 'test-suite :suite-id suite-id :suite-symbol package-symbol)))
	      (if package-symbol
		  (setf (symbol-value package-symbol) test-suite))
	      (pushnew suite-id *suites*)
	      (setf (gethash suite-id *suite-instances*) test-suite)
	      (format t "*suites* => ~S~%" *suites*) ; TODO: Need a logging package
	      test-suite))))))


(declaim (ftype (function (symbol test-suite)) add-test))

(defun add-test (test-symbol suite)
  (pushnew test-symbol (slot-value suite 'suite-tests)))

(defun get-test-ids (suite)
  (reverse (suite-tests suite)))

(defmacro do-test-ids ((id-var suite) &body body)
  `(dolist (,id-var (get-test-ids ,suite))
     ,@body))


(defgeneric get-suite (suite-designator)
  (:documentation "
   Get a `TEST-SUITE' object from a test suite designator.

   A test suite designator might be:

   - A keyword (as returned by `SUITE-ID'.
   - A string.
   - An instance of `TEST-SUITE'.
   - A symbol to which the `TEST-SUITE' is bound, like the symbols automatically
     generated in `*SUITES-PACKAGE*'.
"))

(defmethod get-suite ((suite-id symbol))
  (if (eq (type-of suite-id) 'keyword)
      (gethash suite-id *suite-instances*)
      (let ((suite (gethash suite-id *suite-instances*)))
	(assert (typep suite 'test-suite))
	suite)))

(defmethod get-suite ((suite test-suite))
  suite)

(let ((keyword-package (find-package :keyword)))
  
  (defmethod get-suite ((suite-package package))
    (let* ((package-name (package-name suite-package))
	   (suite-id (find-symbol package-name keyword-package)))
      (assert suite-id nil "~S is not a test suite" suite-package)
      (let ((test-suite (gethash suite-id *suite-instances* nil)))
	(assert test-suite nil "~S is not a test suite" suite-package)
	test-suite))))

;;; * Tags: GET-SUITE-TAGS -------------------------------------------------------------------------

(defgeneric get-suite-tags (suite)
  (:documentation "Get tags of SUITE. Always includes the package name as a keyword symbol"))

(defmethod get-suite-tags ((suite test-suite))
  (list (suite-id suite)))

(defmethod get-suite-tags ((suite symbol))
  (get-suite-tags (get-suite suite)))

(defmethod get-suite-tags ((suite package))
  (get-suite-tags (get-suite suite)))

  
